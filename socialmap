#!/usr/bin/env perl

BEGIN {

    if ( -x '/usr/bin/sqlite3' ) {

        system 'sqlite3 socialmap.db < lib/Model/Schema.sql'
          unless -e 'socialmap.db';
    }
}

use Mojolicious::Lite;
use REST::Google::Search qw/ LOCAL IMAGES /;
use GIS::Distance;

use lib 'lib';
use Model::Schema;

my $MODEL = Model::Schema->connect('dbi:SQLite:socialmap.db');

#### Pages...

get '/' => sub {

    my $self = shift;
    $self->session( page => 'welcome' );

    $self->session('user_id')
      ? $self->render( 'users',   layout => 'default' )
      : $self->render( 'welcome', layout => 'default' );

} => 'welcome';

get '/users' => sub {

    my $self = shift;
    $self->session( page => 'users' );

    return $self->redirect_to('welcome') unless $self->session('user_id');

    my @users =
      $MODEL->resultset('User')->search( undef, { order_by => 'user_id ASC' } );

    $self->{'users'} = \@users;

    $self->render( 'users', layout => 'default' );

} => 'users';

get '/places' => sub {

    my $self = shift;
    $self->session( page => 'places' );

    return $self->redirect_to('welcome') unless $self->session('user_id');

    my @places = $MODEL->resultset('Place')->search(
        undef,

        {
            order_by => 'place_id DESC',
            prefetch => 'images',
        }
    );

    for my $place (@places) {

        my $gis = GIS::Distance->new();

        my $distance = $gis->distance(
            $self->session('lat'), $self->session('lng') => $place->lat,
            $place->lng
        );

        # Display all known places within a 50 mile radius.
        if ( $distance->meters <= (50 * 1609.344) ) {

            push @{ $self->{'places'} }, $place;
        }
    }

    $self->render( 'places', layout => 'default' );

} => 'places';

get '/events' => sub {

    my $self = shift;
    $self->session( page => 'events' );

    return $self->redirect_to('welcome') unless $self->session('user_id');

    my @events =
      $MODEL->resultset('Event')
      ->search( undef, { order_by => 'event_id DESC' } );

    $self->{'events'} = \@events;

    $self->render( 'events', layout => 'default' );

} => 'events';

any '/:page/:id' => [ page => qr/\w+/, id => qr/\d+/ ] => sub {

    my $self = shift;

    if ( $self->param('page') eq 'users' ) {

        return $self->redirect_to('welcome') unless $self->session('user_id');

        my @users =
          $MODEL->resultset('User')
          ->search( { user_id => $self->param('id') } );

        $self->{'users'} = \@users;
        $self->render( 'users', layout => 'default' );
    }

    elsif ( $self->param('page') eq 'places' ) {

        my @places = $MODEL->resultset('Place')->search(

            { place_id => $self->param('id') },

            {
                order_by => 'place_id DESC',
                prefetch => 'images',
            }
        );

        $self->{'places'} = \@places;

        $self->render( 'places', layout => 'default' );
    }

    elsif ( $self->param('page') eq 'events' ) {

        my @events =
          $MODEL->resultset('Event')
          ->search( { event_id => $self->param('id') } );

        $self->{'events'} = \@events;
        $self->render( 'events', layout => 'default' );
    }

};

#### Functions...

get '/session' => sub {

    my $self = shift;

    if ( $self->param('facebook_id') ) {

        my @users = $MODEL->resultset('User')->search();

        my $no_match;

        # This is temporary; I've sinced learned a DBIC approach to the same problem.

        for my $user (@users) {

            $no_match++
              unless $self->param('facebook_id') eq $user->facebook_id;
        }

        if ( $no_match == scalar @users ) {

            REST::Google::Search->service(LOCAL);

            my $response =
              REST::Google::Search->new( q => $self->param('location') );

            unless ( $response->responseStatus != 200 ) {

                my $data    = $response->responseData;
                my @results = $data->results;

                foreach my $result (@results) {

                    my $user = $MODEL->resultset('User')->create(

                        {
                            facebook_id => $self->param('facebook_id'),
                            name        => $self->param('name'),
                            first_name  => $self->param('first_name'),
                            last_name   => $self->param('last_name'),
                            gender      => $self->param('gender'),
                            birthday    => $self->param('birthday'),
                            email       => $self->param('email'),
                            timezone    => $self->param('timezone'),
                            location    => $self->param('location'),
                            city        => $result->city,
                            region      => $result->region,
                            country     => $result->country,
                            lat         => $result->lat,
                            lng         => $result->lng,
                        }
                    );
                }
            }
        }

        for my $user (@users) {

            if ( $user->facebook_id eq $self->param('facebook_id') ) {

                $self->session(

                    user_id     => $user->user_id,
                    facebook_id => $user->facebook_id,
                    name        => $user->name,
                    first_name  => $user->first_name,
                    last_name   => $user->last_name,
                    gender      => $user->gender,
                    birthday    => $user->birthday,
                    email       => $user->email,
                    timezone    => $user->timezone,
                    location    => $user->location,
                    city        => $user->city,
                    region      => $user->region,
                    country     => $user->country,
                    lat         => $user->lat,
                    lng         => $user->lng,
                );

                return $self->redirect_to( $self->session('page') );
            }
        }
    }
    elsif ( $self->param('expires') ) {

        $self->session( expires => $self->param('expires') );
    }

    return $self->redirect_to( $self->session('page') );

} => 'session';

#### Forms...

post '/location' => sub {

    my $self = shift;

    if ( $self->param('location') ) {

        REST::Google::Search->service(LOCAL);

        my $response =
          REST::Google::Search->new( q => $self->param('location') );

        unless ( $response->responseStatus != 200 ) {

            my $data    = $response->responseData;
            my @results = $data->results;

            foreach my $result (@results) {

                $self->session(

                    location => ''
                      . $result->city . ', '
                      . $result->region . '',
                    city    => $result->city,
                    region  => $result->region,
                    country => $result->country,
                    lat     => $result->lat,
                    lng     => $result->lng,

                );
            }
        }
    }

    return $self->redirect_to( $self->session('page') );

} => 'location';

post '/post' => sub {

    my $self = shift;

    my ( $sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst ) =
      localtime(time);

    if ( $self->param('table') eq 'place' ) {

        REST::Google::Search->service(LOCAL);

        my $response =
          REST::Google::Search->new(
            q => $self->param('title') . ' ' . $self->param('location') );

        my $data    = $response->responseData;
        my @results = $data->results;

        foreach my $result (@results) {

            my $place = $MODEL->resultset('Place')->create(

                {
                    category       => $self->param('category'),
                    title          => $result->titleNoFormatting,
                    body           => $self->param('body'),
                    date           => "$year-$mon-$mday",
                    time           => "$hour:$min:$sec",
                    street_address => $result->streetAddress,
                    city           => $result->city,
                    region         => $result->region,
                    country        => $result->country,
                    lat            => $result->lat,
                    lng            => $result->lng,
                    user           => $self->session('user_id'),
                }
            );

            my $new_place =
              $MODEL->resultset('Place')
              ->search( { lat => $result->lat, lng => $result->lng } )->first;

            REST::Google::Search->service(IMAGES);

            my $response =
              REST::Google::Search->new( q => $new_place->title . ' '
                  . $new_place->city . ' '
                  . $new_place->region );

            unless ( $response->responseStatus != 200 ) {

                my $data = $response->responseData;

                my @results = $data->results;

                foreach my $result (@results) {

                    my $image = $MODEL->resultset('PlaceImage')->create(

                        {
                            title   => $result->titleNoFormatting,
                            content => $result->contentNoFormatting,
                            url     => $result->url,
                            width   => $result->width,
                            height  => $result->height,
                            place   => $new_place->place_id,

                        }
                    );
                }
            }
        }

        return $self->redirect_to('places');
    }

    if ( $self->param('table') eq 'event' ) {

        my $event = $MODEL->resultset('Event')->create(

            {
                category => $self->param('category'),
                title    => $self->param('title'),
                body     => $self->param('body'),
                date     => "$year-$mon-$mday",
                time     => "$hour:$min:$sec",
                user     => $self->session('user_id'),
            }
        );

        return $self->redirect_to('events');
    }

    return $self->redirect_to( $self->session('page') );

} => 'post';

app->secret('SocialMAP Rocks!');
app->start;
