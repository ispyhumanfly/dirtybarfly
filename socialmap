#!/usr/bin/env perl

BEGIN {

    if (-x '/usr/bin/sqlite3') {

        system 'sqlite3 socialmap.db < lib/Model/Schema.sql'
          unless -e 'socialmap.db';
    }
}

use Mojolicious::Lite;
use REST::Google::Search qw/ LOCAL /;
use GIS::Distance;

use lib 'lib';
use Model::Schema;

my $MODEL = Model::Schema->connect('dbi:SQLite:socialmap.db');

get '/' => sub {

    my $self = shift;

    return $self->redirect_to('dashboard', layout => 'default')
      if $self->session('user_id');

    return $self->render('welcome', layout => 'default')
      if not $self->session('user_id');

} => 'welcome';

### Dashboard

get '/dashboard' => sub {

    my $self = shift;
    $self->session(page => 'dashboard');

    return $self->redirect_to('welcome') unless $self->session('user_id');

    my @users =
      $MODEL->resultset('User')->search(undef, {order_by => 'user_id ASC'});

    $self->{'users'} = \@users;

    $self->render('dashboard', layout => 'default');

} => 'dashboard';

#any '/dashboard/update/:type/:id' => [type => qr/\w+/, id => qr/\d+/] => sub {
#
#    my $self = shift;
#
#    my ($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst) =
#      localtime(time);
#
#    if ($self->param('type') eq 'account') {
#
#        $MODEL->resultset('User')->search({user_id => $self->param('id')})
#          ->update(
#            {   name       => $self->session('name'),
#                first_name => $self->param('first_name'),
#                last_name  => $self->param('last_name'),
#                gender     => $self->param('gender'),
#                birthday   => $self->param('birthday'),
#                email      => $self->param('email'),
#                timezone   => $self->param('timezone'),
#                city       => $self->param('city'),
#                region     => $self->param('region'),
#                country    => $self->param('country'),
#                avatar     => $self->param('avatar')
#            }
#          );
#
#    }
#    else {
#
#        return $self->redirect_to('dashboard');
#    }
#};

### Users

get '/users' => sub {

    my $self = shift;
    $self->session(page => 'users');

    return $self->redirect_to('welcome') unless $self->session('user_id');

    my @users = $MODEL->resultset('User')->search(
        undef,

        {   order_by => 'user_id DESC',
            prefetch => ['places', 'events'],
        }
    );

    for my $user (@users) {

        my $gis = GIS::Distance->new();

        my $distance = $gis->distance(
            $self->session('lat'), $self->session('lng') => $user->lat,
            $user->lng
        );

        # Display all known places within a 50 mile radius.
        if ($distance->meters <= (50 * 1609.344)) {

            push @{$self->{'users'}}, $user;
        }
    }

    $self->render('blank', layout => 'default')
      unless exists $self->{'users'};

    $self->render('users', layout => 'default');

} => 'users';

### Places

get '/places' => sub {

    my $self = shift;
    $self->session(page => 'places');

    return $self->redirect_to('welcome') unless $self->session('user_id');

    my @places = $MODEL->resultset('Place')->search(
        undef,

        {   order_by => 'place_id DESC',
            prefetch => 'events',
        }
    );

    for my $place (@places) {

        my $gis = GIS::Distance->new();

        my $distance = $gis->distance(
            $self->session('lat'), $self->session('lng') => $place->lat,
            $place->lng
        );

        # Display all known places within a 50 mile radius.
        if ($distance->meters <= (50 * 1609.344)) {

            push @{$self->{'places'}}, $place;
        }
    }

    $self->render('blank', layout => 'default')
      unless exists $self->{'places'};

    $self->render('places', layout => 'default');

} => 'places';

any '/places/submit/:type' => [type => qr/\w+/] => sub {

    my $self = shift;

    my ($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst) =
      localtime(time);

    if ($self->param('type') eq 'place') {

        my $place = $MODEL->resultset('Place')->create(

            {   category       => $self->param('category'),
                title          => $self->param('title'),
                date           => "$year-$mon-$mday",
                time           => "$hour:$min:$sec",
                street_address => $self->param('street_address'),
                city           => $self->param('city'),
                region         => $self->param('region'),
                country        => $self->param('country'),
                lat            => $self->param('lat'),
                lng            => $self->param('lng'),
                popularity     => 0,
                user           => $self->session('user_id'),
            }
        );
    }

    if ($self->param('type') eq 'event') {

        my $event = $MODEL->resultset('PlaceEvent')->create(

            {   category   => $self->param('category'),
                title      => $self->param('title'),
                place      => $self->param('place'),
                start_date => $self->param('start_date'),
                stop_date  => $self->param('stop_date'),
                start_time => $self->param('start_time'),
                stop_time  => $self->param('stop_time'),
                about      => $self->param('about'),
                date       => "$year-$mon-$mday",
                time       => "$hour:$min:$sec",
                popularity => 0,
                user       => $self->session('user_id'),
            }
        );
    }

    return $self->redirect_to('places');

};

### Specific Page Information

get '/:page/:id' => [page => qr/\w+/, id => qr/\d+/] => sub {

    my $self = shift;

    if ($self->param('page') eq 'users') {

        return $self->redirect_to('welcome') unless $self->session('user_id');

        my @users =
          $MODEL->resultset('User')->search({user_id => $self->param('id')});

        $self->{'users'} = \@users;
        $self->render('users', layout => 'default');
    }

    elsif ($self->param('page') eq 'places') {

        my @places = $MODEL->resultset('Place')->search(

            {place_id => $self->param('id')},

            {   order_by => 'place_id DESC',
                prefetch => 'events',
            }
        );

        $self->{'places'} = \@places;
        $self->render('places', layout => 'default');
    }

};

#### Forms...

get '/login' => sub {

    my $self = shift;

    if (    ($self->param('name'))
        and ($self->param('email'))
        and ($self->param('location')))
    {

        REST::Google::Search->service(LOCAL);

        my $response =
          REST::Google::Search->new(q => $self->param('location'));

        if ($response->responseStatus == 200) {

            my $data    = $response->responseData;
            my @results = $data->results;

            foreach my $result (@results) {

                my $new_user = $MODEL->resultset('User')->find_or_create(

                    {   name    => $self->param('name'),
                        email   => $self->param('email'),
                        city    => $result->city,
                        region  => $result->region,
                        country => $result->country,
                        avatar  => '/avatar.png',
                        about   => 'I love to use SocialMAP.',
                        lat     => $result->lat,
                        lng     => $result->lng,
                    }
                );

                my $session_user = $MODEL->resultset('User')->search(
                    {   name   => $self->param('name'),
                        email  => $self->param('email'),
                        city   => $result->city,
                        region => $result->region
                    }
                )->first;

                $self->session(

                    user_id => $session_user->user_id,
                    name    => $session_user->name,
                    email   => $session_user->email,
                    city    => $session_user->city,
                    region  => $session_user->region,
                    country => $session_user->country,
                    lat     => $session_user->lat,
                    lng     => $session_user->lng,
                );
            }
        }
        else {

            return $self->redirect_to('welcome');
        }
    }

    return $self->redirect_to('dashboard');

} => 'login';

get '/logout' => sub {

    my $self = shift;

    $self->session(expires => 1);
    $self->redirect_to('welcome');

} => 'logout';

post '/location' => sub {

    my $self = shift;

    if ($self->param('location')) {

        REST::Google::Search->service(LOCAL);

        my $response =
          REST::Google::Search->new(q => $self->param('location'));

        if ($response->responseStatus == 200) {

            my $data    = $response->responseData;
            my @results = $data->results;

            foreach my $result (@results) {

                $self->session(

                    city    => $result->city,
                    region  => $result->region,
                    country => $result->country,
                    lat     => $result->lat,
                    lng     => $result->lng,

                );
            }
        }
    }

    return $self->redirect_to($self->session('page'));

} => 'location';

app->secret('Insurgent Software Rocks!');
app->start;
